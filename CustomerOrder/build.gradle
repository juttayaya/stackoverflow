buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "spring-boot"

jar {
    baseName = "customerOrder"
    version = "0.1"
}

repositories {
    mavenCentral()
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases"}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations{
    jpametamodel
}

dependencies {
    jpametamodel("org.hibernate:hibernate-jpamodelgen")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.h2database:h2")
    testCompile("junit:junit")
}

sourceSets {
    generated.java.srcDirs = ["src/main/generated"]
}

task generateJpaMetaModel(type: JavaCompile, group: "build") {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.jpametamodel
    options.compilerArgs = ["-proc:only"]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava.dependsOn generateJpaMetaModel
compileJava.source sourceSets.generated.java, sourceSets.main.java

task wrapper(type: Wrapper) {
    gradleVersion = "2.3"
}